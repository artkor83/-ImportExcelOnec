
&НаКлиенте
Функция КлючЭталон(КлючНастроекОсновнойПоУмолчанию) Экспорт
	
	КлючЭталон = КлючНастроекОсновнойПоУмолчанию;
	Если ПустаяСтрока(КлючЭталон) Тогда
		Возврат ""
	КонецЕсли;
	
	// Отрежем время от ключа на конце.
	ЭтоТолькоЦифры = Истина; НайденКоврик = Ложь;
	Ном = СтрДлина(КлючЭталон);
	Пока Истина Цикл
		Если Сред(КлючЭталон, Ном,1) = "_" Тогда
			НайденКоврик = Истина;
			Прервать;
		КонецЕсли;
		Если НЕ ЭтоЦифра(Сред(КлючЭталон, Ном,1)) Тогда
			ЭтоТолькоЦифры = Ложь;
			Прервать;
		КонецЕсли;
		Ном = Ном - 1;
		Если Ном = 0 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НайденКоврик И ЭтоТолькоЦифры Тогда
		КлючЭталон = Лев(КлючЭталон, Ном-1);
	КонецЕсли;

	Возврат КлючЭталон
	
КонецФункции

&НаКлиенте
Функция ЕстьАналоги(КлючЭталон) Экспорт
	
	Если ПустаяСтрока(КлючЭталон) Тогда
		Возврат Ложь
	КонецЕсли;

	ЕстьАналоги = Ложь;
	Для Каждого СтрокаДанных Из ТаблицаИстория Цикл
		Если ЭтоАналог(СтрокаДанных.КлючНастроек, КлючЭталон) Тогда
			ЕстьАналоги = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьАналоги
КонецФункции

&НаКлиенте
Функция ЭтоАналог(КлючНастроек, КлючЭталон) Экспорт
	
	Если ПустаяСтрока(КлючЭталон) Тогда
		Возврат Ложь
	КонецЕсли;

	ЭтоАналог = Ложь;

	Если ВРег(КлючНастроек) = ВРЕГ(КлючЭталон) Тогда
		ЭтоАналог = Истина;
		
	Иначе
		
		Если Лев(ВРег(КлючНастроек), СтрДлина(КлючЭталон)+1) = ВРЕГ(КлючЭталон) + "_" Тогда
			
			Постфикс = Сред(КлючНастроек, СтрДлина(КлючЭталон)+2);
			
			// Постфикс должен быть датой в виде "ггггММдд" ("20220224") или "ггггММдд<час><мин><сек>".
			Если Лев(Постфикс, 2) = "20" И (СтрДлина(Постфикс) = 8 ИЛИ СтрДлина(Постфикс) = 12 ИЛИ СтрДлина(Постфикс) = 16) Тогда
				
				ВсеЧисла = Истина;
				Для Ном = 1 По СтрДлина(Постфикс) Цикл
					Если НЕ ВладелецФормы.ЭтоЧисло(Сред(Постфикс,Ном,1)) Тогда
						ВсеЧисла = Ложь; Прервать;
					КонецЕсли;
				КонецЦикла;
				Если ВсеЧисла Тогда
					ЭтоАналог = Истина;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЭтоАналог
	
КонецФункции

&НаКлиенте
Процедура НовыйКлючПоЭталону(КлючНастроекОсновнойПоУмолчанию) Экспорт
	
	КлючЭталон = КлючЭталон(КлючНастроекОсновнойПоУмолчанию);
	Если ПустаяСтрока(КлючЭталон) Тогда
		КлючНастроекНовый = "";
		Возврат
	КонецЕсли;
	
	ЕстьАналоги = ЕстьАналоги(КлючЭталон);
	
	КлючНастроекНовый = КлючЭталон;
	Если ЕстьАналоги Тогда
		КлючНастроекНовый = КлючНастроекНовый + "_" + Формат(ТекущаяДата(), "ДФ=yyyyMMddhhmmss")
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаИсторияОбновить() Экспорт
	
	ТаблицаИстория.Очистить();
	Для Каждого СтрокаДанных Из ВладелецФормы.ТаблицаИстория Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаИстория.Добавить(), СтрокаДанных);
	КонецЦикла;
	
КонецПроцедуры
			
&НаКлиенте
Процедура ПоискДанныхИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Если Текст = "" Тогда
		Элементы.ТаблицаИстория.ОтборСтрок = Неопределено
	Иначе
		Элементы.ТаблицаИстория.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючНастроек", Текст)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискДанныхОчистка(Элемент, СтандартнаяОбработка)
	
	ПоискДанныхИзменениеТекстаРедактирования(Элемент, "", СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТаблицаИсторияОбновить();
	
	Если Режим = "" Тогда
		Режим = "Создать новую";
	КонецЕсли;
	РежимПриИзменении(Неопределено);	ОбновитьИмя(Неопределено);
	
	// Подставляем ключ основной настройки для поиска.
	ПоискДанных = ВладелецФормы.КлючНастроекОсновнойПоУмолчанию();
	ПоискДанныхИзменениеТекстаРедактирования(Неопределено, ПоискДанных, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимПриИзменении(Элемент)
	
	Элементы.КлючНастроекНовый.Доступность = (Режим = "Создать новую");
	Элементы.ОбновитьИмя.Доступность = (Режим = "Создать новую");
	
	Элементы.ПоискДанных.Доступность = (Режим = "Выбор из списка");
	Элементы.ТаблицаИстория.Доступность = (Режим = "Выбор из списка");

	Элементы.ПоискДанных.Доступность = (Режим = "Выбор из списка");
	
	Элементы.ФормаВыбрать.Заголовок = ?(Режим = "Создать новую", "Создать", "Выбрать");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИмя(Команда)
	
	НовыйКлючПоЭталону(ВладелецФормы.КлючНастроекОсновнойПоУмолчанию());
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоЦифра(Символ)
	
	Возврат (Найти("0123456789", Символ) > 0);
	
КонецФункции

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ТаблицаИстория.ТекущиеДанные;
	
	Если Режим = "Создать новую" Тогда
		//НоваяСтрока = ВладелецФормы.ТаблицаИстория.Добавить();
		//НоваяСтрока.КлючНастроек = КлючНастроекНовый;
		//НоваяСтрока.ДатаЗаписи   = ТекущаяДата();  
		//НоваяСтрока.Описание     = ВладелецФормы.ОписаниеНастройки();  
		
		ВладелецФормы.КлючНастроекОсновной = КлючНастроекНовый;
		ВладелецФормы.ХранилищеОН_СохранитьВсе();
		ВладелецФормы.ОбновитьКлючНастроекОсновной(Ложь, Ложь, КлючНастроекНовый, Истина);
	Иначе
		Если ТекущиеДанные = Неопределено Тогда
			Сообщить("Не выбрана строка");
			Возврат
		КонецЕсли;
		//ВладелецФормы.КлючНастроекОсновной 			 = ТекущиеДанные.КлючНастроек;
		//ВладелецФормы.Параметры.КлючНастроекОсновной = ТекущиеДанные.КлючНастроек;
		ВладелецФормы.ОбновитьКлючНастроекОсновной(Ложь, Ложь, ТекущиеДанные.КлючНастроек, Истина);
	КонецЕсли;
	
	//ВладелецФормы.ТаблицаИсторияУстановитьТекущую(ВладелецФормы.ТаблицаИстория); 
	ВладелецФормы.Модифицированность = Истина;
	Закрыть();

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Режим = "Создать новую" Тогда
		
		ПроверяемыеРеквизиты.Добавить("КлючНастроекНовый");
		
		Для Каждого СтрокаДанных Из ТаблицаИстория Цикл
			Если ВРег(СтрокаДанных.КлючНастроек) = ВРЕГ(КлючНастроекНовый) Тогда
				Сообщить("Данное наименование уже есть в истории");
				Отказ = Истина
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		Если ТаблицаИстория.Количество() = 0 Тогда
			Сообщить("Не выбрана строка истории");
			Отказ = Истина
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
